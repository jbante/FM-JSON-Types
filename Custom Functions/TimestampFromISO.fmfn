/*******************************************************************************
 * TimestampFromISO ( iso8601Timestamp )
 * Parses timestamp data formatted according to ISO 8601, and casts it to a
 * FileMaker timestamp. For timestamps with limited precision, the first time in
 * the specified period will be returned.
 *
 * @parameter iso8601Timestamp: text encoding a time in ISO 8601 format
 *
 * @return A timestamp
 *
 * @uses DateFromISO ( iso8601Date )
 * @uses TimeFromISO ( iso8601Time )
 *
 * @history 2017-04-16 - Jeremy Bante <http://scr.im/fugue> - Created
 * @history 2017-05-11 - Jeremy Bante <http://scr.im/fugue> - Improving error
 * result.
 * @history 2017-10-02 - Jeremy Bante <http://scr.im/fugue> - Tolerating spaces
 * as timestamp delimiters.
 *
 * @see https://en.wikipedia.org/wiki/ISO_8601
 ******************************************************************************/

 Let ( [
	_date = DateFromISO ( iso8601Timestamp ) ;
	_delimiterIsPresent =
		Position ( iso8601Timestamp ; "T" ; 1 ; 1 ) > 0
		or Position ( iso8601Timestamp ; " " ; 1 ; 1 ) > 0 ;
	_time =
		If ( _delimiterIsPresent ;
			TimeFromISO ( iso8601Timestamp ) ;
			/* Else */ 0
		) ;
	_error =
		Left ( _date ; 1 ) = "?"
		or Left ( _time ; 1 ) = "?"
		or _delimiterIsPresent and ( IsEmpty ( _date ) or IsEmpty ( _time ) )
] ;
	If ( _error ;
		"? Not a valid ISO 8601 timestampÂ¶  " & iso8601Timestamp ;
	/* Else */
		Timestamp ( _date ; _time )
	)
)